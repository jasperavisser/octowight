buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE"
        classpath "se.transmode.gradle:gradle-docker:1.2"
    }
}

apply plugin: "idea"

allprojects {
    group "nl.haploid"
    version "1.0-SNAPSHOT"

    apply plugin: "idea"
    apply plugin: "scala"

    apply plugin: "io.spring.dependency-management"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencyManagement {
        dependencies {
            dependency("com.101tec:zkclient:0.3") {
                exclude "org.slf4j:slf4j-log4j12"
                exclude "log4j:log4j"
            }
            dependency "com.fasterxml.jackson.core:jackson-annotations:2.5.1"
            dependency "com.fasterxml.jackson.module:jackson-module-scala_2.11:2.5.1"
            dependency "com.yammer.metrics:metrics-core:2.2.0"
            dependency("org.apache.kafka:kafka_2.11:0.8.2.1") {
                exclude "org.slf4j:slf4j-log4j12"
            }
            dependency "org.apache.kafka:kafka-clients:0.8.2.1"
            dependency "org.easymock:easymock:3.3.1"
            dependency "org.scala-lang:scala-library:2.11.6"
            dependency "org.scala-lang:scala-compiler:2.11.6"
            dependency "org.scalatest:scalatest_2.11:2.2.4"
        }

        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:1.2.5.RELEASE"
        }
    }

    dependencies {
        compile "com.fasterxml.jackson.core:jackson-annotations"
        compile "com.fasterxml.jackson.module:jackson-module-scala_2.11"
        compile "org.scala-lang:scala-library"
        compile "org.scala-lang:scala-compiler"
        compile "org.springframework.boot:spring-boot-starter"
        testCompile "junit:junit"
        testCompile "org.easymock:easymock"
        testCompile "org.scalatest:scalatest_2.11"
        testCompile "org.springframework:spring-test"
    }
}

subprojects {

    test {
        exclude "**/*IT.class"
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }

    task IT(type: Test) {
        include "**/*IT.class"
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }
}
